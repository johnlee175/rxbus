apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    task.baseName = 'rxbus'
    task.version = '1.0.0'
    task.doLast {
        copy {
            from task.archivePath
            into new File(project.project(':app').projectDir, "libs")
        }
    }
    artifacts.add('archives', task);
}

dependencies {
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'
    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup:otto:1.3.8'
    testCompile 'org.greenrobot:eventbus:3.0.0'
    testCompile 'org.robolectric:robolectric:3.1.1'
    testCompile 'org.robolectric:shadows-support-v4:3.1.1'
}
